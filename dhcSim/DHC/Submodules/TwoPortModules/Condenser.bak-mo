within dhcSim.DHC.Submodules.TwoPortModules;
model Condenser
  "Two port passive condenser model. Port_a is considered the cold, port_b is considered the hot side."
  extends Buildings.Fluid.Interfaces.PartialTwoPortInterface;
  extends Buildings.Fluid.Interfaces.TwoPortFlowResistanceParameters(final
      computeFlowResistance=(abs(dp_nominal) > Modelica.Constants.eps));
  extends
    dhcSim.DHC.Submodules.TwoPortModules.Interfaces.TwoPortModulesParameters(
      T_start=Medium.T_default);

  // Efficiency
  parameter Real etaCarnot_nominal(unit="1")=0.5
    "Carnot effectiveness (=COP/COP_Carnot)"
    annotation (Dialog(group="Efficiency"));
  parameter dhcSim.Fluid.Types.OutletTemperature effInpCon=dhcSim.Fluid.Types.OutletTemperature.gradient
    "Define condensator temperature"
    annotation (Dialog(group="Efficiency"), Evaluate=True);
  parameter Modelica.SIunits.Temperature TGrad(displayUnit="K")=6
    "Temperature gradient" annotation(Dialog(group="Efficiency", enable=effInpCon
           == dhcSim.Fluid.Types.OutletTemperature.gradient));

  parameter Boolean use_Q_flow_in=false "=true to use QLoad port" annotation (Dialog(group="Thermal load"));
  parameter Boolean use_TEva_in=false "=true to use TEva_in port" annotation (Dialog(group="Thermal load"));
  parameter Boolean use_TCon_in=false "=true to use TEva_in port" annotation (Dialog(group="Thermal load"));
  parameter Modelica.SIunits.HeatFlowRate QLoadSet=0.0 "Constant thermal load" annotation (Dialog(group="Thermal load", enable=not use_Q_flow_in));
  parameter Modelica.SIunits.Temperature TEvaSet
     "Evaporator temperature used to compute efficiency"
                                                        annotation(Dialog(enable=not use_TEva_in));
  parameter Modelica.SIunits.Temperature TConSet
     "Condenser temperature used to compute efficiency"
                                                       annotation(Dialog(enable=not use_TCon_in));

  Modelica.SIunits.HeatFlowRate Q_flow_abs "Absolute heat flow rate";

  Modelica.Fluid.Interfaces.FluidPort_a port_a(
    redeclare package Medium = Medium) annotation (Placement(transformation(extent={{-110,-10},{-90,10}})));
  Modelica.Fluid.Interfaces.FluidPort_b port_b(
    redeclare package Medium = Medium) annotation (Placement(transformation(extent={{90,-10},{110,10}})));
  dhcSim.DHC.Submodules.CarnotMachines.Condenser.Condenser con(
    redeclare final package Medium = Medium,
    final allowFlowReversal=allowFlowReversal,
    final T_start=T_start,
    final energyDynamics=energyDynamics,
    final etaCarnot_nominal=etaCarnot_nominal,
    final m_flow_nominal=m_flow_nominal,
    final TGrad=TGrad,
    final effInpCon=effInpCon,
    final use_Q_flow_in=true,
    final m_flow_small=m_flow_small,
    final from_dp=from_dp,
    final dp_nominal=dp_nominal,
    final linearizeFlowResistance=linearizeFlowResistance,
    final deltaM=deltaM,
    final use_TEva_in=true,
    final use_TCon_in=true)
    annotation (Placement(transformation(extent={{-10,-10},{10,10}})));
  Modelica.Blocks.Interfaces.RealInput QEva_in(unit="W") if  use_Q_flow_in
    annotation (Placement(transformation(
        extent={{-20,-20},{20,20}},
        rotation=90,
        origin={0,-120}), iconTransformation(
        extent={{-20,-20},{20,20}},
        rotation=90,
        origin={0,-80})));
  Modelica.Blocks.Sources.RealExpression QLoad_exp(y=Q_flow_abs) annotation (
      Placement(transformation(
        extent={{10,-10},{-10,10}},
        rotation=180,
        origin={-30,-40})));
  Modelica.Blocks.Interfaces.RealOutput P_out "Electrical power" annotation (
      Placement(transformation(
        extent={{-10,-10},{10,10}},
        rotation=90,
        origin={0,110})));
  Modelica.Blocks.Interfaces.RealOutput QEva_flow_out "Evaporator heat flow"
    annotation (Placement(transformation(
        extent={{-10,-10},{10,10}},
        rotation=90,
        origin={60,110})));
  Modelica.Blocks.Interfaces.RealOutput QCon_flow_out "Condensator heat flow"
    annotation (Placement(transformation(
        extent={{-10,-10},{10,10}},
        rotation=90,
        origin={-60,110})));
  Modelica.Blocks.Interfaces.RealOutput dp_out "Pressure drop" annotation (
      Placement(transformation(
        extent={{-10,-10},{10,10}},
        rotation=0,
        origin={110,-60})));
  Modelica.Blocks.Sources.RealExpression dp_exp(y=dp) annotation (Placement(
        transformation(
        extent={{10,-10},{-10,10}},
        rotation=180,
        origin={70,-60})));
  Modelica.Blocks.Sources.RealExpression TEva_exp(y=TEva_internal)
                                                              annotation (
      Placement(transformation(
        extent={{10,-10},{-10,10}},
        rotation=180,
        origin={-30,-24})));
  Modelica.Blocks.Interfaces.RealInput TEva_in(unit="K") if  use_TEva_in
    annotation (Placement(transformation(
        extent={{-20,-20},{20,20}},
        rotation=90,
        origin={-60,-120}), iconTransformation(
        extent={{-20,-20},{20,20}},
        rotation=90,
        origin={-60,-80})));
  Modelica.Blocks.Interfaces.RealInput TCon_in(unit="K") if  use_TCon_in
    annotation (Placement(transformation(
        extent={{-20,-20},{20,20}},
        rotation=90,
        origin={60,-120}),  iconTransformation(
        extent={{-20,-20},{20,20}},
        rotation=90,
        origin={60,-80})));
  Modelica.Blocks.Sources.RealExpression TCon_exp(y=TCon_internal) annotation (
      Placement(transformation(
        extent={{10,-10},{-10,10}},
        rotation=0,
        origin={32,-24})));
  Buildings.Fluid.FixedResistances.PressureDrop res(
    redeclare final replaceable package Medium = Medium,
    final allowFlowReversal=allowFlowReversal,
    final m_flow_nominal=m_flow_nominal,
    final show_T=show_T,
    final from_dp=from_dp,
    final dp_nominal=dp_nominal,
    final linearized=linearizeFlowResistance,
    final deltaM=deltaM)
    annotation (Placement(transformation(extent={{-60,-10},{-40,10}})));

protected
  Modelica.Blocks.Interfaces.RealInput TEva_internal(unit="K");
  Modelica.Blocks.Interfaces.RealInput TCon_internal(unit="K");
  Modelica.Blocks.Interfaces.RealInput QLoad_internal(unit="W");
  parameter Modelica.SIunits.SpecificEnthalpy deltah=cp_default*m_flow_small*
      0.01 "Small value for deltah used for regularization";
  parameter Medium.ThermodynamicState state_default=Medium.setState_pTX(
      T=Medium.T_default,
      p=Medium.p_default,
      X=Medium.X_default[1:Medium.nXi]) "Default state";
  parameter Modelica.SIunits.Density rho_default=Medium.density(state=state_default);
  parameter Modelica.SIunits.DynamicViscosity mu_default=
      Medium.dynamicViscosity(state=state_default)
    "Dynamic viscosity at nominal condition";
  parameter Modelica.SIunits.SpecificHeatCapacity cp_default=
      Medium.specificHeatCapacityCp(state=state_default)
    "Specific heat capacity at default medium state";
equation
  connect(QEva_in, QLoad_internal);
  if not use_Q_flow_in then
    QLoad_internal = QLoadSet;
  end if;

  connect(TEva_in, TEva_internal);
  if not use_TEva_in then
    TEva_internal = TEvaSet;
  end if;

  connect(TCon_in, TCon_internal);
  if not use_TCon_in then
    TCon_internal = TConSet;
  end if;

  Q_flow_abs = abs(QLoad_internal);
  connect(con.port_b, port_b)
    annotation (Line(points={{10,0},{100,0}},        color={0,127,255}));
  connect(con.P_out, P_out) annotation (Line(points={{11,-6},{40,-6},{40,60},{0,
          60},{0,110}}, color={0,0,127}));
  connect(con.QEva_flow_out, QEva_flow_out) annotation (Line(points={{11,-9},{
          60,-9},{60,2},{60,110}}, color={0,0,127}));
  connect(con.QCon_flow_out, QCon_flow_out) annotation (Line(points={{11,-3},{
          20,-3},{20,40},{-60,40},{-60,110}}, color={0,0,127}));
  connect(dp_exp.y, dp_out)
    annotation (Line(points={{81,-60},{91.5,-60},{110,-60}}, color={0,0,127}));
  connect(QLoad_exp.y, con.QEva_in)
    annotation (Line(points={{-19,-40},{0,-40},{0,-12}}, color={0,0,127}));
  connect(TEva_exp.y, con.TEva_in)
    annotation (Line(points={{-19,-24},{-6,-24},{-6,-12}}, color={0,0,127}));
  connect(TCon_exp.y, con.TCon_in)
    annotation (Line(points={{21,-24},{6,-24},{6,-12}}, color={0,0,127}));
  connect(res.port_b, con.port_a)
    annotation (Line(points={{-40,0},{-10,0}}, color={0,127,255}));
  connect(res.port_a, port_a)
    annotation (Line(points={{-60,0},{-100,0}}, color={0,127,255}));
  annotation (defaultComponentName="con",
    Diagram(coordinateSystem(preserveAspectRatio=false, extent={{-100,-100},{100,
            100}})),
    Icon(coordinateSystem(preserveAspectRatio=false, extent={{-100,-100},{100,100}}),
        graphics={
        Rectangle(
          extent={{-70,60},{70,-60}},
          lineColor={0,0,255},
          pattern=LinePattern.None,
          fillColor={95,95,95},
          fillPattern=FillPattern.Solid),
        Rectangle(
          extent={{-100,6},{100,-6}},
          lineColor={0,0,255},
          pattern=LinePattern.None,
          fillColor={0,0,255},
          fillPattern=FillPattern.Solid),
        Rectangle(
          extent={{0,-6},{100,6}},
          lineColor={0,0,255},
          pattern=LinePattern.None,
          fillColor={255,0,0},
          fillPattern=FillPattern.Solid),
        Line(points={{-60,100},{-60,60}}, color={28,108,200}),
        Line(points={{0,100},{0,60}}, color={28,108,200}),
        Line(points={{60,100},{60,60}}, color={28,108,200})}));
end Condenser;
